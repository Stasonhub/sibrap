include <frame0-config.scad>
include <../abs/halfcube.scad>

// Рама для 3д-принтера SibRap
// под любую толщину оргстекла
// Генерируются чертежи в dxf-формате
// Для установки толщины отредактируйте файл frame0-config.scad

// габаритные размеры
// ширина
y1 = 418;

// отрезанный уголок внизу у ножек
// длина
x2 = 25;
// ширина
y2 = 73;

// отрезанный уголок вверху
// вырезы в центре под углом
// угол наклона
a1 = 60;
// ширина
y3 = 280;
// длина
x3 = y3*1/tan(a1);

// габаритная длина
x1 = 2*x3+76;
echo(x1);

// длина
//x3 = (x1-76)/2;
// ширина
//y3 = 280;

// ширина ножки
x31 = 30;

// вырез внизу между ножками
// длина
x4 = x1-(x2+x31)*2;
// ширина
y4 = y2+65-40-50;

// вырез в центре - верхняя грань
// длина
x5 = 52;
// ширина
y5 = 240;
// отступ сверху до выреза
h5 = 40;

// вырез в центре - нижняя грань
// длина
x6 = x4;
// ширина
y6 = 40;

// вырезы в центре под углом
// угол наклона
a2 = a1;
// ширина
y7 = y5;
// длина
x7 = y7*1/tan(a2);

// вырезы в центре под углом - нижняя грань
// длина
x8 = x7+x5/2-x6/2;
// ширина
y8 = y6;

// паз для верхней балки
// ширина
x9 = dh;
// длина
y9 = 20;
// отступ слева до оси выреза
h9 = 12+x9/2;

// паз для нижней балки
// ширина
x10 = 44;
// длина
y10 = dh;

// шпильки
// радиус отверстий под шпильки
r1 = 4;
// расстояние между верхними шпильками
x11 = 362;
// расстояние по горизонтали между нижними и верхними шпильками
x12 = 16;
// расстояние по вертикали между нижними и верхними шпильками
y12 = 25;
// расстояние снизу до верхних шпилек
y11 = 62+y12;

module triangles() {
    // вырезы слева
    halfcube([x2,y2,dh],"z");
    translate([0,y1,0])rotate([0,0,-90])halfcube([y3,x3,dh],"z");
    // вырез внизу между ножками
    translate([x2+x31,0,0])cube([x4,y4,dh]);
    // вырез в центре
    translate([(x1-x5)/2,y1-h5-y5,0]) {
        // верхняя грань
        cube([x5,y5,dh]);
        // вырез под углом
        translate([x5,0,0])halfcube([x7,y7,dh],"z");
    }
    translate([x2+x31,y4+50,0]) {
        // нижняя грань
        cube([x6,y6,dh]);
        // вырез под углом
        translate([0,y6,0])rotate([0,0,180])halfcube([x8,y8,dh],"z");
    }
    translate([(x1-x11)/2,y11,0]){
        cylinder(r=r1,h=dh);
        translate([x12,-y12,0])cylinder(r=r1,h=dh);
    }
}

module main() {
    difference() {
        cube([x1,y1,dh]);
        triangles();
        translate([x1,0,0]) mirror([1,0,0]) triangles();
        // паз для верхней балки
        translate([x3+h9-x9/2,y1-y9,0])cube([x9,y9,dh]);
        // паз для нижней балки
        translate([(x1-x10)/2,y4,0])cube([x10,y10,dh]);
    }
}

projection() main();